apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'io.fabric'

def getDpiId(String density) {

    def DPI_ID = ['null': 0, 'mdpi': 1, 'hdpi': 2, 'xhdpi': 3, 'xxhdpi': 4]
    if (density != null)
        return DPI_ID.get(density);
    return 0;
}

def getAPKArchiveFolder(variant) {
    return "/Android/launching-android/" + "V" + variant.mergedFlavor.versionCode;
}

def allowSplit() {
    return gradle.startParameter.taskNames.contains("assembleRelease") &&
            !project.hasProperty('tkpdBuildType');
}

def allowDistribution() {
    if (project.hasProperty('tkpdBuildType') && tkpdBuildType == "hockeyapp") {
        return "true";
    }
    return "false";
}

def getTkpdVersionCode() {
    return project.hasProperty('versionCode') ? Integer.parseInt(versionCode) : project.defaultVersionCode;
}

def getTkpdVersionName() {
    return project.hasProperty('versionName') ? versionName : project.defaultVersionName;
}

def renameAPKFile(variant, density, versionCode) {

    def fileName = variant.name.toUpperCase()
    fileName += "-" + variant.mergedFlavor.versionName;
    fileName += "-V" + versionCode;
    fileName += "-" + density;
    fileName += ".apk";
    return fileName;
}

def archiveAPK(variant, output) {

    String density = output.getFilter(com.android.build.OutputFile.DENSITY);
    def originZipAlignedApkFile = output.outputFile;
    def renameAPKFile = renameAPKFile(variant, density, output.versionCodeOverride);
    copy {
        from "$originZipAlignedApkFile"
        into getAPKArchiveFolder(variant)
        rename("$originZipAlignedApkFile.name", "$renameAPKFile")
    }
}

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}


android {

    project.ext {
        defaultVersionCode = 1000404;
        defaultVersionName = "1.0.4";
    }

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    useLibrary 'org.apache.http.legacy'
    sourceSets {

        packagingOptions {
            exclude 'META-INF/LICENSE.txt'
        }

        main {
            java.srcDirs = ['src/main/java']
            resources.srcDirs = ['src/main/java']
            aidl.srcDirs = ['src/main/java']
            renderscript.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['assets', 'src/main/assets', 'src/main/assets/']
        }

        instrumentTest.setRoot('tests')
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            zipAlignEnabled false
            buildConfigField "boolean", "ENABLE_DISTRIBUTION", allowDistribution()
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            buildConfigField "boolean", "ENABLE_DISTRIBUTION", allowDistribution()
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            ext.betaDistributionReleaseNotesFilePath = 'release_notes.txt'
            ext.betaDistributionGroupAliasesFilePath = 'group_beta.txt'
            if (allowSplit()) {
                applicationVariants.all { variant ->
                    variant.outputs.each { output ->
                        String density = output.getFilter(com.android.build.OutputFile.DENSITY);
                        output.versionCodeOverride = (getTkpdVersionCode() * 100) + getDpiId(density);
                    }
                    variant.assemble.doLast {
                        variant.outputs.each { output ->
                            if (output.zipAlign) {
                                archiveAPK(variant, output);
                            }
                        }
                    }
                }
            } else {
                applicationVariants.all { variant ->
                    variant.outputs.each { output ->
                        output.versionCodeOverride = (getTkpdVersionCode() * 100);
                    }
                }
            }
        }
    }

    splits {
        density {
            enable allowSplit()
            exclude "tvdpi", "xxxhdpi", "ldpi" //Add or modify the supported density here`
            compatibleScreens 'small', 'normal', 'large', 'xlarge' //Discouraged to touch this
        }
    }

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        multiDexEnabled = true
        applicationId "com.tokopedia.sellerapp"
        versionCode getTkpdVersionCode()
        versionName getTkpdVersionName()
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
        packagingOptions {
            exclude "lib/arm64-v8a/libgnustl_shared.so"
        }
    }


    dexOptions {
        javaMaxHeapSize "4g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
        resolutionStrategy.force 'com.squareup.okio:okio:1.11.0'
    }

    productFlavors {
        staging {

        }

        live {

        }
    }


}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    /*compile (rootProject.ext.tkpdDependencies.inbox) {
            exclude group: 'com.tokopedia.core'
        }
        compile (rootProject.ext.tkpdDependencies.discovery) {
            exclude group: 'com.tokopedia.core'
        }
        compile (rootProject.ext.tkpdDependencies.transaction) {
            exclude group: 'com.tokopedia.core'
        }
        compile (rootProject.ext.tkpdDependencies.seller) {
            exclude group: 'com.tokopedia.core'
        }
        compile (rootProject.ext.tkpdDependencies.session) {
            exclude group: 'com.tokopedia.core'
        }
        compile (rootProject.ext.tkpdDependecies.transaction) {
        exclude group 'com.tokopedia.core'
        }
        compile (rootProject.ext.tkpdDependencies.core)*/
    compile project(':tkpdpdp')
    compile project(':tkpdtopads')
    compile project(':tkpdgm')
    compile project(':tkpdsession')
    compile project(':tkpdinbox')
    compile project(':tkpddigital')
    compile project(':tkpdtransaction')
    compile project(':tkpdreputation')

    compile(rootProject.ext.miscDependencies.trueCaller) {
        exclude group: 'com.android.support'
    }

    compile('de.hdodenhof:circleimageview:2.1.0') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    compile rootProject.ext.butterKnifeDependencies.butterKnife
    apt rootProject.ext.butterKnifeDependencies.butterKnifeCompiler

    provided rootProject.ext.miscDependencies.javaxAnnotation
    compile rootProject.ext.miscDependencies.dagger
    apt rootProject.ext.miscDependencies.daggerCompiler

    // Required -- JUnit 4 framework
    testCompile rootProject.ext.testDependencies.junit

    // Optional -- Mockito framework
    testCompile rootProject.ext.testDependencies.mockitorCore

    testCompile rootProject.ext.miscDependencies.rxJava

    androidTestCompile rootProject.ext.miscDependencies.rxJava

    androidTestCompile rootProject.ext.testDependencies.uiautomator

    androidTestCompile rootProject.ext.testDependencies.spoon
    androidTestCompile rootProject.ext.testDependencies.androidAnnotation

    androidTestCompile(rootProject.ext.testDependencies.espressoCore, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    androidTestCompile(rootProject.ext.testDependencies.androidTestRunner) {
        exclude module: 'support-annotations'
    }
    androidTestCompile(rootProject.ext.testDependencies.androidRulesVersion) {
        exclude module: 'support-annotations'
    }

    androidTestCompile(rootProject.ext.testDependencies.espressoIntents) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestCompile rootProject.ext.testDependencies.retrofitMock
    androidTestCompile rootProject.ext.testDependencies.retrofitMockWebser

    androidTestApt rootProject.ext.miscDependencies.daggerCompiler
    androidTestCompile rootProject.ext.miscDependencies.dagger
    androidTestCompile rootProject.ext.miscDependencies.gson
    androidTestCompile rootProject.ext.miscDependencies.parcelerApi
    androidTestApt rootProject.ext.miscDependencies.parceler

    androidTestCompile rootProject.ext.testDependencies.dexMaker
    androidTestCompile(rootProject.ext.testDependencies.dexMakerMockito) {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }

    compile rootProject.ext.deeplinkDispatchDependencies.deeplinkDispatch
    apt rootProject.ext.deeplinkDispatchDependencies.deeplinkDispatchProcessor
}
