apply plugin: 'com.android.library'
apply plugin: 'android-apt'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

def getTkpdCore() {
    return project.hasProperty('isArtifact') ?
            rootProject.ext.tkpdDependencies.core : project(':tkpdcore');
}

def getArtifactVersionCode() {
    return project.hasProperty('artifactVersion') ? artifactVersion : '1.0';
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId 'com.tokopedia.tkpdreactnative'
            artifactId 'tkpdreactnative'
            version getArtifactVersionCode()
            artifact("$buildDir/outputs/aar/tkpdreactnative-release.aar")
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.compile.allDependencies.each {
                    if (it.group != null && it.name != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

artifactory {
    contextUrl = rootProject.ext.artifactoryUrl
    publish {
        repository {
            repoKey = 'libs-release-local'

            username = artifactory_username
            password = artifactory_password
        }
        defaults {
            publications('aar')
            publishArtifacts = true

            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}
android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion "25.0.0"

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
}

dependencies {
    compile getTkpdCore()
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'


    compile rootProject.ext.miscDependencies.dagger
    apt rootProject.ext.miscDependencies.daggerCompiler

    compile rootProject.ext.miscDependencies.reactNative
    compile rootProject.ext.miscDependencies.codePushReactNative

}