apply plugin: 'com.android.library'
apply plugin: 'android-apt'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'
apply plugin: 'maven'


buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.jakewharton:butterknife-gradle-plugin:8.4.0'
    }
}

def getArtifactVersionCode() {
    return project.hasProperty('artifactVersion') ? artifactVersion : '1.0';
}

def getTkpdCore() {
    return project.hasProperty('isArtifact') ?
            rootProject.ext.tkpdDependencies.core : project(':tkpdcore');
}

def getTkpdDesign() {
    return project.hasProperty('isArtifact') ?
            rootProject.ext.tkpdDependencies.design : project(':tkpddesign');
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId 'com.tokopedia.events'
            artifactId 'events'
            version getArtifactVersionCode()
            artifact("$buildDir/outputs/aar/tkpdevents-release.aar")
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.compile.allDependencies.each {
                    if (it.group != null && it.name != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

artifactory {
    contextUrl = rootProject.ext.artifactoryUrl
    publish {
        repository {
            repoKey = 'libs-release-local'

            username = artifactory_username
            password = artifactory_password
        }
        defaults {
            publications('aar')
            publishArtifacts = true

            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion "26.0.2"

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
}



dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile getTkpdCore()
    compile getTkpdDesign()

    compile rootProject.ext.butterKnifeDependencies.butterKnife
    apt rootProject.ext.butterKnifeDependencies.butterKnifeCompiler

    compile rootProject.ext.deeplinkDispatchDependencies.deeplinkDispatch
    apt rootProject.ext.deeplinkDispatchDependencies.deeplinkDispatchProcessor

    compile rootProject.ext.miscDependencies.dagger
    apt rootProject.ext.miscDependencies.daggerCompiler

    compile rootProject.ext.supportLibDependencies.recyclerViewV7
    compile rootProject.ext.supportLibDependencies.supportV4
    compile rootProject.ext.supportLibDependencies.appCompactV7
    compile rootProject.ext.supportLibDependencies.supportV13
    compile rootProject.ext.supportLibDependencies.design
    compile rootProject.ext.supportLibDependencies.cardViewV7



    compile 'at.blogc:expandabletextview:1.0.5'
    compile project(path: ':tkpdpayment')
}
