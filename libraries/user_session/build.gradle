apply plugin: 'com.android.library'
apply from: "$rootProject.projectDir/buildconfig/kotlin/module.gradle"
apply plugin: 'io.hansel.preprocessor.module'
apply plugin: 'com.google.protobuf'

buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://maven-hansel.tokopedia.com/maven' }
    }
    dependencies {
        classpath rootProject.ext.miscDependencies.hansel
        classpath rootProject.ext.miscDependencies.protobuf
    }
}

android {
    namespace 'com.tokopedia.user.session'
    testNamespace 'com.tokopedia.user.session.test'
    defaultConfig {
        testInstrumentationRunner "com.tokopedia.test.application.environment.InstrumentationTestRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    buildFeatures {
        buildConfig true
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
}

dependencies {
    implementation rootProject.ext.miscDependencies.javaxInject
    implementation projectOrAar(rootProject.ext.libraries.logger)
    implementation projectOrAar(rootProject.ext.libraries.deviceInfo)
    implementation rootProject.ext.miscDependencies.workManager
    implementation rootProject.ext.miscDependencies.dataStore
    implementation rootProject.ext.miscDependencies.protobufLite
    implementation projectOrAar(rootProject.ext.libraries.encryption)
    androidTestImplementation(rootProject.ext.testDependencies.androidxTest)
    androidTestImplementation(rootProject.ext.testDependencies.workManagerTest)
    androidTestImplementation rootProject.ext.testDependencies.mockkAndroid
    androidTestImplementation rootProject.ext.testDependencies.androidTestRunner
    androidTestImplementation projectOrAar(rootProject.ext.libraries.instrumentation_test)
    androidTestUtil rootProject.ext.testDependencies.orchestrator
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.19.1"
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}

// https://youtrack.jetbrains.com/issue/KT-52761/Kotlin-170-breaks-kapt-processing-for-protobuf-generated-java-sources
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KaptGenerateStubs).configureEach {
    source("$buildDir/generated/source/proto/debug/main/java")
    source("$buildDir/generated/source/proto/release/main/java")
    source("$buildDir/generated/source/proto/debug/java")
    source("$buildDir/generated/source/proto/release/java")
}

tasks.withType(org.jetbrains.kotlin.gradle.internal.KaptWithoutKotlincTask$KaptExecutionWorkAction).configureEach {
    source("$buildDir/generated/source/proto/debug/main/java")
    source("$buildDir/generated/source/proto/release/main/java")
    source("$buildDir/generated/source/proto/debug/java")
    source("$buildDir/generated/source/proto/release/java")
}

project.ext {
    artifactId = 'user_session'
    groupId = 'com.tokopedia.user.session'
    versionName = "0.0.1"
    artifactName = "usersession-release"
}

apply from: '../../publish_local.gradle'
