apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if(project.plugins.findPlugin("android-library")) {
        from android.sourceSets.main.java.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
    }
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId project.groupId
            artifactId project.artifactId
            version project.versionName
            if(project.plugins.findPlugin("android-library")) {
                artifact("$buildDir/outputs/aar/"+project.artifactName+".aar")
            } else{
                artifact("$buildDir/libs/"+project.artifactName+".jar")
            }
            artifact sourcesJar
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                try {
                    configurations.implementation.allDependencies.each { addDependencyNode(dependenciesNode, it) }
                } catch(Exception ignored) {
                    // no op
                }
                try {
                    configurations.debugImplementation.allDependencies.each { addDependencyNode(dependenciesNode, it) }
                } catch(Exception ignored) {
                    // no op
                }
                try {
                    configurations.api.allDependencies.each { addDependencyNode(dependenciesNode, it) }
                } catch(Exception ignored) {
                    // no op
                }
            }
        }
    }
}

private static void addDependencyNode(Node dependencies, Dependency it) {
    if (it.group != null && it.name != null) {
        def dependency = dependencies.appendNode('dependency')
        dependency.appendNode('groupId', it.group)
        dependency.appendNode('version', it.version)
        dependency.appendNode('artifactId', it.name)
    }
}

artifactory {
    contextUrl = rootProject.ext.gcp_artifactory_url
    publish {
        repository {
            repoKey = 'libs-release-local'

            username = gcp_artifactory_username
            password = gcp_artifactory_password
        }
        defaults {
            publications('aar')
            publishArtifacts = true

            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}
