buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:1.7.20"
    }
}

apply plugin: "org.jetbrains.dokka"

import org.jetbrains.dokka.gradle.DokkaMultiModuleTask

def dirName = "tkpd-docs"

tasks.withType(DokkaMultiModuleTask).configureEach {
    outputDirectory = rootProject.file(dirName)
    failOnWarning.set(true)

    String baseConfiguration = """
            {
                "customAssets": ["${file("dokka-templates/assets/logo-icon.svg")}"],
                "customStyleSheets": ["${file("dokka-templates/logo-styles.css")}"],
                "footerMessage": "(c) 2023 Tokopedia Android Engineering Team"
            }
        """

    pluginsMapConfiguration.set(["org.jetbrains.dokka.base.DokkaBase": baseConfiguration])

    dokkaSourceSets.configureEach {
        documentedVisibilities.set([
                DokkaConfiguration.Visibility.PUBLIC,
                DokkaConfiguration.Visibility.PROTECTED
        ])

        perPackageOption {
            matchingRegex.set(".*internal.*")
            suppress.set(true)
        }
    }
}

subprojects {
    afterEvaluate {
        if (tasks.findByName('dokkaHtmlPartial') == null) {
            // If dokka isn't enabled on this module, skip
            return
        }

        tasks.named('dokkaHtmlPartial') {
            dokkaSourceSets.configureEach {
                reportUndocumented.set(true)
                skipEmptyPackages.set(true)
                skipDeprecated.set(true)
                jdkVersion.set(8)

                // Add Android SDK packages
                noAndroidSdkLink.set(false)

                // AndroidX + Kotlin docs
                externalDocumentationLink {
                    url.set(new URL("https://developer.android.com/reference/"))
                    packageListUrl.set(new URL("https://developer.android.com/reference/androidx/package-list"))
                }
                externalDocumentationLink {
                    url.set(new URL("https://developer.android.com/reference/kotlin/"))
                    packageListUrl.set(new URL("https://developer.android.com/reference/kotlin/androidx/package-list"))
                }
            }
        }
    }
}
