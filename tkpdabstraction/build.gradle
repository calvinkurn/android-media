apply plugin: 'com.android.library'
apply plugin: 'android-apt'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'
apply plugin: 'maven'

group = rootProject.ext.groupModule
version = rootProject.ext.tkpdAbstractionVersion

publishing {
    publications {
        aar(MavenPublication) {
            groupId rootProject.ext.groupModule
            artifactId rootProject.ext.tkpdAbstractionArtifact
            version rootProject.ext.tkpdAbstractionVersion
            artifact("$buildDir/outputs/aar/tkpdabstraction-release.aar")
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.compile.allDependencies.each {
                    if (it.group != null && it.name != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

def getTkpdDesign() {
    return project.hasProperty('isArtifact') ?
            rootProject.ext.tkpdDependencies.design : project(':tkpddesign');
}

def getArtifactVersionCode() {
    return project.hasProperty('artifactVersion') ? artifactVersion : '1.0';
}

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        mavenCentral()
        jcenter()
    }
}

artifactory {
    contextUrl = rootProject.ext.artifactoryUrl
    publish {
        repository {
            repoKey = 'libs-release-local'

            username = artifactory_username
            password = artifactory_password
        }
        defaults {
            publications('aar')
            publishArtifacts = true

            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile getTkpdDesign()

    compile rootProject.ext.supportLibDependencies.appCompactV7

    compile rootProject.ext.serviceDependencies.hockeyApp

    compile rootProject.ext.miscDependencies.roundCornerProgressBar
    compile rootProject.ext.miscDependencies.gson
    compile rootProject.ext.miscDependencies.rxAndroid
    compile rootProject.ext.miscDependencies.rxJava
    compile rootProject.ext.miscDependencies.multiDex

    compile rootProject.ext.networkDependencies.glide
    compile rootProject.ext.networkDependencies.converterGson
    compile rootProject.ext.networkDependencies.retrofit
    compile rootProject.ext.networkDependencies.adapterRxJava
    compile rootProject.ext.networkDependencies.loggingInterceptor

    provided rootProject.ext.miscDependencies.javaxAnnotation
    compile rootProject.ext.miscDependencies.dagger
    apt rootProject.ext.miscDependencies.daggerCompiler
}
