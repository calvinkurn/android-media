apply plugin: 'com.android.library'
apply plugin: 'android-apt'
apply plugin: 'maven-publish'
apply plugin: 'maven'

group = rootProject.ext.groupModule
version = rootProject.ext.tkpdMitratoppersVersion

publishing {
    publications {
        aar(MavenPublication) {
            groupId rootProject.ext.groupModule
            artifactId rootProject.ext.tkpdMitratoppersArtifact
            version rootProject.ext.tkpdMitratoppersVersion
            artifact("$buildDir/outputs/aar/tkpdmitratoppers-release.aar")
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.compile.allDependencies.each {
                    if (it.group != null && it.name != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

def getTkpdAbstraction() {
    return project.hasProperty('isArtifact') ?
            rootProject.ext.tkpdDependencies.abstraction : project(':tkpdabstraction');
}

def getTkpdUseCase() {
    return project.hasProperty('isArtifact') ?
            rootProject.ext.tkpdDependencies.usecase : project(':tkpdusecase');
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        vectorDrawables.useSupportLibrary = true

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
}

apt {
    arguments {
        targetModuleName 'TkpdMitraToppers'
    }
}

dependencies {
    compile getTkpdAbstraction()
    compile getTkpdUseCase()

    compile rootProject.ext.supportLibDependencies.supportV4

    provided rootProject.ext.miscDependencies.javaxAnnotation
    compile rootProject.ext.miscDependencies.dagger
    apt rootProject.ext.miscDependencies.daggerCompiler

    compile rootProject.ext.debugToolsDependencies.stetho
}
