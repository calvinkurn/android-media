// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: 'buildconfig/dependencies.gradle'

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'http://central.maven.org/maven2/'}
        mavenCentral()
        jcenter()
        google()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'com.uber:okbuck:0.37.2'
        classpath ('com.android.tools.build:gradle:3.1.2') {
            exclude module: 'proguard-gradle'
        }
        classpath('net.sf.proguard:proguard-gradle:6.0.3') {
            force = true
        }
        classpath("org.jfrog.buildinfo:build-info-extractor-gradle:4.0.1") {
            exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        }
        classpath 'com.apollographql.apollo:gradle-plugin:0.3.0'
        if (!project.hasProperty("disable-performance-plugin")) {
            classpath('com.google.firebase:firebase-plugins:1.1.1') {
                exclude group: 'com.google.guava', module: 'guava-jdk5'
            }
        }
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.40"
    }
}

allprojects {
    project.apply from: rootProject.file('buildconfig/dependencies.gradle')
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        google()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://www.jitpack.io" }
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://dl.bintray.com/guardian/android' }
        maven { url "https://maven.google.com" }
        maven { url 'https://mymavenrepo.com/repo/e01iL1W9Gdxw73PGI9LA' }
        maven {
            url "${artifactory_url}" + "/libs-release-local";
            credentials {
                username = "${artifactory_username}"
                password = "${artifactory_password}"
            }
        }
        maven { url "$rootDir/node_modules/react-native/android" }
        flatDir {
            dirs '../aars'
        }
        maven { url 'https://hansel.io/maven' }
    }
}

apply plugin: 'com.uber.okbuck'

okbuck {
    buildToolVersion = "27.0.3"
    target = "android-27"
    linearAllocHardLimit = [
            customerapp: 10130000,
            sellerapp: 10130000
    ]
    extraBuckOpts = [
            'customerappDebug': [
                    "android_binary": ["trim_resource_ids = True"] as Set
            ]
            ,'sellerappDebug': [
                    "android_binary": ["trim_resource_ids = True"] as Set
            ]
    ]

    intellij {
        sources = true
    }

    lint {
        disabled = true
    }

    wrapper {
        repo = 'https://github.com/facebook/buck.git'
    }

    lint {
        version = "26.0.1"
    }

}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

ext {
    artifactoryUrl = "${artifactory_url}"
}

afterEvaluate {
    tasks.withType(PublishToMavenRepository).all { publishTask ->
        publishTask.onlyIf { task ->
            if (project.plugins.hasPlugin('com.android.application')) {
                task.enabled = false
                task.group = null
                return false
            }
            return true
        }
    }
}

subprojects {
    project.configurations.all {
        resolutionStrategy {
            dependencySubstitution {
                substitute module('com.squareup.okhttp3:okhttp') with module(rootProject.ext.networkDependencies.okHttp)
                substitute module('com.squareup.okio:okio') with module('com.squareup.okio:okio:1.13.0')
                substitute module('com.google.code.findbugs:jsr305') with module('com.google.code.findbugs:jsr305:1.3.9')
                substitute module('com.android.support:support-annotations') with module('com.android.support:support-annotations:26.0.2')
                substitute module('com.android.support:multidex') with module('com.android.support:multidex:1.0.3')
                substitute module('com.android.support:support-v13') with module('com.android.support:support-v13:26.0.2')
                substitute module('com.android.support:support-v4') with module('com.android.support:support-v4:26.0.2')
                substitute module('com.android.support:support-compat') with module('com.android.support:support-compat:26.0.2')
            }
        }
    }

}


gradle.buildFinished {
    "zip -d .okbuck/cache/org.hamcrest.hamcrest-core-1.3.jar LICENSE.txt".execute()
    "zip -d .okbuck/cache/org.hamcrest.hamcrest-library-1.3.jar LICENSE.txt".execute()
    "zip -d .okbuck/cache/org.hamcrest.hamcrest-integration-1.3.jar LICENSE.txt".execute()
}

