allprojects {
    project.apply from: rootProject.file('buildconfig/dependencies/dependencies.gradle')
    repositories {
        maven {
            url = "$rootDir/localMavenRepository"
        }
        google()
        mavenCentral()
        mavenLocal()
        maven {
            url "${gcp_artifactory_url}" + "/libs-release-local";
            credentials {
                username = "${gcp_artifactory_username}"
                password = "${gcp_artifactory_password}"
            }
            allowInsecureProtocol = true
        }
        maven {
            url "${artifactory_url}" + "/libs-release-local";
            credentials {
                username = "${artifactory_username}"
                password = "${artifactory_password}"
            }
            allowInsecureProtocol = true
        }
        maven {
            url "https://jitpack.io"
            credentials { username authToken }
        }
        flatDir {
            dirs rootProject.file('aars')
        }
        maven { url 'https://maven-hansel.tokopedia.com/maven' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    artifactoryUrl = "${artifactory_url}"
}

afterEvaluate {
    tasks.withType(PublishToMavenRepository).all { publishTask ->
        publishTask.onlyIf { task ->
            if (project.plugins.hasPlugin('com.android.application')) {
                task.enabled = false
                task.group = null
                return false
            }
            return true
        }
    }
}

task("runJacoco")
task("runUnitTest")
task("runLintReport")
task("runLintPr")

def lintPrTaskFile = rootProject.file("tasks.txt")

subprojects {
    tasks.withType(Test) {
        if (project.hasProperty("SPEK_TIMEOUT")) {
            systemProperty "SPEK_TIMEOUT", project.getProperty("SPEK_TIMEOUT")
            maxParallelForks = Runtime.runtime.availableProcessors()
        }
    }
    project.configurations.all {
        resolutionStrategy {
            dependencySubstitution {
                force "androidx.appcompat:appcompat:${supportLibraryVersion}"
                force("org.objenesis:objenesis:2.6")
                substitute module('org.jetbrains.kotlin:kotlin-stdlib') with module(rootProject.ext.supportLibDependencies.kotlinSupport)
                substitute module('org.jetbrains.kotlin:kotlin-stdlib-common') with module("org.jetbrains.kotlin:kotlin-stdlib-common:${kotlinVersion}")
                substitute module('com.squareup.okio:okio') with module('com.squareup.okio:okio:2.6.0')
                substitute module('com.google.code.findbugs:jsr305') with module('com.google.code.findbugs:jsr305:1.3.9')
                substitute module('androidx.annotation:annotation') with module("androidx.annotation:annotation:${supportLibraryVersion}")
                substitute module('androidx.multidex:multidex') with module("androidx.multidex:multidex:${multiDexVersion}")
                substitute module('androidx.legacy:legacy-support-v13') with module("androidx.legacy:legacy-support-v13:${supportLegacyLibraryVersion}")
                substitute module('androidx.legacy:legacy-support-v4') with module("androidx.legacy:legacy-support-v4:${supportLegacyLibraryVersion}")
                substitute module('androidx.cardview:cardview') with module("androidx.cardview:cardview:${supportLegacyLibraryVersion}")
                substitute module('androidx.core:core') with module("androidx.core:core:${androidxCoreVersion}")
                substitute module('androidx.arch.core:core-runtime') with module("androidx.arch.core:core-runtime:${archCoreVersion}")
                substitute module('androidx.arch.core:core-common') with module("androidx.arch.core:core-common:${archCoreVersion}")
                substitute module('androidx.lifecycle:lifecycle-runtime') with module("androidx.lifecycle:lifecycle-runtime:${lifeCycleVersion}")
                substitute module('androidx.lifecycle:lifecycle-viewmodel') with module("androidx.lifecycle:lifecycle-viewmodel:${lifeCycleVersion}")
                substitute module('androidx.lifecycle:lifecycle-livedata-core') with module("androidx.lifecycle:lifecycle-livedata-core:${lifeCycleVersion}")
                substitute module('androidx.fragment:fragment') with module("androidx.fragment:fragment:${androidxFragment}")
                substitute module('androidx.lifecycle:lifecycle-service') with module("androidx.lifecycle:lifecycle-service:${lifeCycleVersion}")
                substitute module('com.google.android.material:material') with module("com.google.android.material:material:${materialVersion}")
                substitute module('androidx.constraintlayout:constraintlayout') with module("androidx.constraintlayout:constraintlayout:${constraintLayoutVersion}")
                substitute module('org.apache.commons:commons-lang3') with module('org.apache.commons:commons-lang3:3.5')
                substitute module('com.google.android.gms:play-services-base') with module("com.google.android.gms:play-services-base:${playServiceBaseVersion}")
                substitute module('com.google.android.gms:play-services-measurement-base') with module("com.google.android.gms:play-services-measurement-base:${playServiceMeasurementBase}")
                substitute module('com.google.android.gms:play-services-base') with module("com.google.android.gms:play-services-base:${playServiceBase}")
                substitute module('androidx.media:media') with module("androidx.media:media:1.3.0")
                substitute module('org.jetbrains.kotlin:kotlin-reflect') with module("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")
                substitute module('androidx.sqlite:sqlite-framework') with module("androidx.sqlite:sqlite-framework:${sqliteFrameworkVersion}")
                substitute module('com.google.android.gms:play-services-ads-identifier') with module("com.google.android.gms:play-services-ads-identifier:${playServiceAds}")
                substitute module('org.jetbrains.kotlin:kotlin-stdlib-jdk7') with module("org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlinVersion}")
                substitute module('org.jetbrains.kotlin:kotlin-stdlib-jdk8') with module("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}")
                substitute module('com.google.code.gson:gson') with module("com.google.code.gson:gson:$gsonDebugVersion")
                substitute module("androidx.room:room-runtime") with module("androidx.room:room-runtime:$roomVersion")
                substitute module("androidx.room:room-compiler") with module("androidx.room:room-compiler:$roomVersion")

                if (!rootProject.ext.debugToolsEnabled) {
                    substitute module(rootProject.ext.debugToolsDependencies.chuck) with module(rootProject.ext.debugToolsDependencies.chuckNoop)
                    substitute module(rootProject.ext.tkpdInternalLibDependencies.fakeresponse) with module(rootProject.ext.tkpdInternalLibDependencies.fakeresponseNoOp)
                }
            }
        }
    }
    project.afterEvaluate {
        def isLibrary = project.plugins.hasPlugin("com.android.library")
        def isApp = project.plugins.hasPlugin("com.android.application")
        def isDF = project.plugins.hasPlugin("com.android.dynamic-feature")
        if (project.tasks.findByName("jacocoTestReport")!= null) {
            def task = project.path + ":jacocoTestReport"
            rootProject.tasks.runJacoco.dependsOn(task)
            if (isApp) {
                task = project.path + ":testLiveProdDebugUnitTest"
                rootProject.tasks.runUnitTest.dependsOn(task)
            } else if (isLibrary && !isDF ){
                task = project.path + ":testDebugUnitTest"
                rootProject.tasks.runUnitTest.dependsOn(task)
            }
        }
        if (project.tasks.findByName("lint")!= null) {
            def lintTask = null
            if (isApp) {
                lintTask = project.path + ":lintLiveDevDebug"
            } else if (isLibrary && !isDF ){
                lintTask = project.path + ":lintDebug"
            }
            if (lintTask != null) {
                rootProject.tasks.runLintReport.dependsOn(lintTask)
            }
            if (lintPrTaskFile.exists() && lintPrTaskFile.readLines().contains(lintTask)) {
                rootProject.tasks.runLintPr.dependsOn(lintTask)
            }
        }
        try {
            if (isLibrary) {
                project.apply from: "$rootDir/buildconfig/aar/publish.gradle"
            }
        }catch(Exception ignored) {

        }
    }
}